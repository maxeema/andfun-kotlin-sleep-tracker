/*...*/

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 29
    defaultConfig {
        resConfigs "en"
        applicationId "maxeem.america.sleep"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("Object", "databaseImpl", "null")
    }
    signingConfigs {
        debug {
            def debugKeystoreProps = new Properties()
            debugKeystoreProps.load(new FileInputStream(rootProject.file('key.properties')))
            keyAlias debugKeystoreProps['keyAlias']
            keyPassword debugKeystoreProps['keyPassword']
            storeFile file(debugKeystoreProps['storeFile'])
            storePassword debugKeystoreProps['storePassword']
        }
    }
    buildTypes {
        release {
            applicationIdSuffix '.demo'
            versionNameSuffix '-demo'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
        fake {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.fake'
            versionNameSuffix '-fake'
            buildConfigField("Object", "databaseImpl",
                    "maxeem.america.sleep.data.NightsDatabaseFakeImpl.Companion.getInstance()")
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    sourceSets {
        fake {
            java {
                srcDirs 'src/fake/java', 'src/fake/kotlin'
            }
        }
        main.java.srcDirs += 'src/main/kotlin'
    }
    buildToolsVersion = '29.0.2'
}

dependencies {
    kapt "androidx.room:room-compiler:2.2.2"
    kapt "com.github.bumptech.glide:compiler:4.10.0"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.collection:collection-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta3'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.0-rc02'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-rc02'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-rc02'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-rc02'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0-rc02'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:1.0.0-rc02'
    implementation 'androidx.navigation:navigation-common-ktx:2.2.0-rc02'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.0-rc02'
    implementation 'androidx.navigation:navigation-runtime-ktx:2.2.0-rc02'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.0-rc02'
    implementation "androidx.room:room-runtime:2.2.2"
    implementation "androidx.room:room-ktx:2.2.2"
    implementation "com.google.android.material:material:1.2.0-alpha02"
    implementation "com.github.bumptech.glide:glide:4.10.0"
    implementation "org.jetbrains.anko:anko-commons:0.10.8"
    implementation "org.jetbrains.anko:anko-design:0.10.8"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0"
    implementation "org.koin:koin-android:2.0.1"
    implementation "org.koin:koin-android-viewmodel:2.0.1"
    // Testing dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
